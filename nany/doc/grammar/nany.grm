"Name"     = 'Nany'
"Author"   = 'Bruno BORRI, Damien GERARD'
"Version"  = '0.1'
"About"    = 'Grammar for the Nany language, from the Yuni project team'
"Character Mapping" = 'Unicode'
"Case Sensitive" = 'True'
"Start Symbol" = <Program>

! -------------------------------------------------
! Character Sets
! -------------------------------------------------

{ID Head}      = {Letter} + {Letter Extended}
{ID Tail}      = {Alphanumeric} + [_]
{String Chars} = {Printable} + {HT} - ["\]
{Char Chars}   = {Printable} - ['']
{Hex Digit}    = {Digit} + [abcdef] + [ABCDEF]

! -------------------------------------------------
! Terminals
! -------------------------------------------------

Identifier    = {ID Head}{ID Tail}*
BooleanLiteral= 'true' | 'false'
StringLiteral = '"' ( {String Chars} | '\' {Printable} )* '"'
DecLiteral     = {Digit}+            ( [UuLl] | [Uu][Ll] | [Ll][Uu] )?
HexLiteral     = '0'[xX]{Hex Digit}+ ( [UuLl] | [Uu][Ll] | [Ll][Uu] )?
RealLiteral    = {Digit}*'.'{Digit}+ ( [FfDd] )?
CharLiteral    = '' ( {Char Chars} | '\'{Printable} ) ''
TimeLiteral    = ( {Digit}+ | {Digit}*'.'{Digit}+ ) ( 'ms' | 's' | 'm' | 'h' )
ConstQualifier = 'const'
TypeQualifier  = ConstQualifier | 'ref' | 'volatile'
VisibilityQualifier = 'public' | 'protected' | 'private' | 'published'
OptimQualifier = 'immutable' | 'stable' | 'threadunsafe'
BuiltInType    = 'void' | 'any' | 'int' | 'uint' | 'int8' | 'uint8' | 'int16' | 'uint16' | 'int32' | 'uint32' | 'int64' | 'uint64' | 'char' | 'float' | 'double' | 'bool' | 'string'

! -------------------------------------------------
! Comments
! -------------------------------------------------

Comment Line = '#' | '//'
Comment Start = '#*' | '/*'
Comment End = '*#' | '*/'

! -------------------------------------------------
! Program / Unit
! -------------------------------------------------

! The grammar starts below
<Program> ::= <Unit Declaration> <Dependencies> <Declaration List>

<Unit Declaration> ::= <Optional Visibility Qualifier> 'unit' Identifier ';'
               | 'program' Identifier ';'

<Dependencies> ::= <Dependency> <Dependencies>
               | ! Empty

<Dependency>   ::= 'uses' Identifier <Dependency Continued> ';'

<Dependency Continued> ::= '.' Identifier <Dependency Continued>
               | ! Empty

<Declaration List> ::= <Optional Visibility Qualifier> <Function Declaration> <Declaration List>
               | <Optional Visibility Qualifier> <Class Declaration> <Declaration List>
               | <Optional Visibility Qualifier> <Workflow Declaration> <Declaration List>
               | <Optional Visibility Qualifier> <Enum Declaration> <Declaration List>
               | <Optional Visibility Qualifier> <Typedef> ';' <Declaration List>
               | ! Empty

! -------------------------------------------------
! Literals
! -------------------------------------------------

<Literal> ::= BooleanLiteral
               |  DecLiteral
               |  HexLiteral
               |  RealLiteral
               |  TimeLiteral
               |  CharLiteral
               |  StringLiteral
               |  BuiltInType
               |  'nil'
               |  'self'

! -------------------------------------------------
! Classes
! -------------------------------------------------

<Class Declaration> ::= 'class' Identifier <Optional Type Parameters> <In Block> <Out Block> '{' <Class Content> '}'

! TODO
<Class Content> ::= VisibilityQualifier <Class Content>
               | <Method Declaration> <Class Content>
               | <Attribute Declaration> ';' <Class Content>
               | <Property Declaration> ';' <Class Content>
               | <Class Declaration> <Class Content> ! Nested class
               | <Typedef> ';' <Class Content>
               | ! Empty

<Property Declaration> ::= 'property' Identifier <Typing> <Assignment> <Property Callbacks>
               | 'property' Identifier <Assignment> <Property Callbacks>
               | 'property' Identifier <Typing> <Property Callbacks>

<Property Callbacks> ::= 'read' <SingleThread Exp> <Property Callbacks>
               | 'write' <SingleThread Exp> <Property Callbacks>
               | ! Empty

! Strict version, works fine and is clean but not powerful enough
!<Attribute Declaration> ::= Identifier ':' <Type Qualifiers> Identifier <Optional Assignment>
!               | ConstQualifier Identifier ':=' <Expression>
!               | Identifier ':=' <Expression>
! Loose version, very powerful but requires a keyword and a lot of work in the type-checking
<Attribute Declaration> ::= 'attribute' Identifier <Typing> <Assignment>
               | 'attribute' Identifier <Assignment>
               | 'attribute' Identifier <Typing>
               | ConstQualifier 'attribute' Identifier <Typing> <Assignment>
               | ConstQualifier 'attribute' Identifier <Assignment>
               | ConstQualifier 'attribute' Identifier <Typing>

<Assignment> ::= ':=' <Expression>

<Typing> ::= ':' <Type Qualifiers> <Simple Exp>

! -------------------------------------------------
! Workflows
! -------------------------------------------------

<Workflow Declaration> ::= 'workflow' Identifier '{' <Workflow Content> '}'

<Workflow Content> ::= <State Block> <Transition Block>

<State Block> ::= 'states' <Workflow States>
               | ! Empty

<Workflow States> ::= 'default' Identifier ';' <Workflow States>
               | 'state' Identifier ';' <Workflow States>
               | ! Empty

<Transition Block> ::= 'transitions' <Workflow Transitions>
               | ! Empty

<Workflow Transitions> ::= 'default' 'allow' ';' <Workflow Transitions>
               | 'default' 'forbid' ';' <Workflow Transitions>
               | 'allow' <Workflow Permissions> '=>' <Workflow Permissions> ';' <Workflow Transitions>
               | 'forbid' <Workflow Permissions> '=>' <Workflow Permissions> ';' <Workflow Transitions>
               | ! Empty

<Workflow Permission> ::= '*'
               | Identifier
               | '+' Identifier
               | '-' Identifier

<Workflow Permissions> ::= <Workflow Permission> ',' <Workflow Permissions>
               | <Workflow Permission>

! -------------------------------------------------
! Enums
! -------------------------------------------------

<Enum Declaration> ::= 'enum' Identifier '{' <Enum Content> '}'

<Enum Content> ::= Identifier ',' <Enum Content>
               | Identifier
               | ! Empty


! -------------------------------------------------
! Functions and methods
! -------------------------------------------------

<Function Declaration> ::= <Optional Optim Qualifier> 'function' Identifier <Optional Type Parameters> <Optional Parameters> <Return Type Declaration> <In Block> <Out Block> '{' <Expression> '}'
               | <Optional Optim Qualifier> 'function' Identifier <Optional Type Parameters> <Optional Parameters> <Return Type Declaration> <In Block> <Out Block> '{' '}' ! Empty body

<Method Declaration> ::= <Optional Optim Qualifier> 'method' Identifier <Optional Type Parameters> <Optional Parameters> <Return Type Declaration> <In Block> <Out Block> '{' <Expression> '}'
               | <Optional Optim Qualifier> 'method' Identifier <Optional Type Parameters> <Optional Parameters> <Return Type Declaration> <In Block> <Out Block> '{' '}' ! Empty body

<Return Type Declaration> ::= ':' <Expression>
               | ! Empty

<Optional Parameters> ::= '(' <Parameter List> ')'
               | ! Empty

<Parameter List> ::= Identifier <Typing> <Assignment> <Parameter List Continued>
               | Identifier <Assignment> <Parameter List Continued>
               | Identifier <Typing> <Parameter List Continued>
               | Identifier <Parameter List Continued>
               | ! Empty

<Parameter List Continued> ::= ',' <Parameter List>
               | ! Empty

<Type Qualifiers> ::= TypeQualifier <Type Qualifiers Continued>
               | ! Empty

<Type Qualifiers Continued> ::= TypeQualifier <Type Qualifiers Continued>
               | ! Empty

<Optional Optim Qualifier> ::= OptimQualifier
               | ! Empty

<Optional Visibility Qualifier> ::= VisibilityQualifier
               | ! Empty

<Argument List> ::= <Expression> <Argument List Continued>
               | ! Empty

<Argument List Continued> ::= ',' <Expression> <Argument List Continued>
               | ! Empty

<Typedef> ::= 'type' Identifier ':=' <Expression>

! -------------------------------------------------
! Constraint blocks
! -------------------------------------------------

<In Block> ::= 'in' <Expression>
               | ! Empty

<Out Block> ::= 'out' <Expression>
               | ! Empty

! -------------------------------------------------
! Types
! -------------------------------------------------

<Optional Type Parameters> ::= <Type Parameters>
               | ! Empty

! This is for generics declarations
<Type Parameters> ::= '<' Identifier <Type Parameters Continued> '>'
               | '<' Identifier ':=' <Expression> <Type Parameters Continued> '>'

<Type Parameters Continued> ::= ',' Identifier <Type Parameters Continued>
               | ',' Identifier ':=' <Expression> <Type Parameters Continued>
               | ! Empty

! This is for generics instanciations
<Type Arguments> ::= <Expression> <Type Arguments Continued>
               | ! Empty

<Type Arguments Continued> ::= ',' <Expression> <Type Parameters Continued>
               | ! Empty

! -------------------------------------------------
! Expressions
! -------------------------------------------------

<Expression>   ::= <Possibly Parallel Exp> <Expression List>

<Expression List> ::= ';' <Expression>
               | ! Empty

<Possibly Parallel Exp> ::= <SingleThread Exp>
               | '&' <SingleThread Exp>
               | 'async' <SingleThread Exp>
               | 'sync' <SingleThread Exp>

<SingleThread Exp> ::= <Assignment Exp>

<Assignment Exp> ::= <Assignment Exp> ':=' <Is Exp>
               | <Local Declaration Exp>

<Local Declaration Exp> ::= <Value> <Typing>
               | <Is Exp>

<Is Exp>    ::= <Is Exp> 'is' <Simple Exp>
               | <Simple Exp>

<Simple Exp> ::= <Binary Exp>
               | 'new' <Simple Exp>
               | <Typedef>
               | 'return' <SingleThread Exp>
               | 'break'
               | 'continue'
               | 'if' <Expression> 'then' <Expression> <Else Expression>
               | 'while' <Expression> 'do' <Expression>
               | 'for' Identifier 'in' <Expression> 'do' <Expression>
               | 'for' Identifier 'in' <Expression> 'order' ':' <Expression> 'packedby' ':' <Expression> 'do' <Expression>
               | 'for' Identifier 'in' <Expression> 'order' 'do' <Expression>
               | 'timeout' <Expression> 'do' <Expression>
               | 'timeout' <Expression> 'do' <Expression> 'else' <Expression>
               | <Function Declaration>
               | '{' <Expression> '}'

<Else Expression> ::= 'else' <Expression>
               | ! Empty

<Binary Exp> ::= <Binary Exp> '|' <Xor Exp> ! Constraint
               |  <Xor Exp>

<Xor Exp>      ::= <Xor Exp> 'xor' <Or Exp> ! Boolean XOR
               |  <Or Exp>

<Or Exp>      ::= <Or Exp> 'or' <And Exp> ! Boolean Or
               |  <And Exp>

<And Exp>     ::= <And Exp> 'and' <Equal Exp> ! Boolean And
               |  <Equal Exp>

<Equal Exp>   ::= <Equal Exp> '='  <Compare Exp>    ! Equal
               |  <Equal Exp> '!=' <Compare Exp>    ! Not equal
               |  <Compare Exp>

<Compare Exp> ::= <Compare Exp> '<' <Regexp Exp>
               |  <Compare Exp> '>'  <Regexp Exp> 
               |  <Compare Exp> '<=' <Regexp Exp> 
               |  <Compare Exp> '>=' <Regexp Exp>
               |  <Regexp Exp>

! Is the priority correct ?
<Regexp Exp>  ::= <Regexp Exp> '~' <Shift Exp>
               |  <Shift Exp>

<Shift Exp>   ::= <Shift Exp> '<<' <Add Exp>
               |  <Shift Exp> '>>' <Add Exp>
               |  <Add Exp>

<Add Exp>     ::= <Add Exp> '+' <Mult Exp>
               |  <Add Exp> '-' <Mult Exp>
               |  <Mult Exp> 

<Mult Exp>    ::= <Mult Exp> '*' <Power Exp>
               |  <Mult Exp> '/' <Power Exp>
               |  <Mult Exp> '%' <Power Exp>
               |  <Power Exp>

<Power Exp>   ::= <Power Exp> '^' <As Exp>
               | <As Exp>

<As Exp>    ::= <As Exp> 'as' <Typeof Exp>
               | <Typeof Exp>

<Typeof Exp> ::= 'typeof' <Negate Exp>
               | <Negate Exp>

<Negate Exp>  ::= '-' <Value> 
               |  '--' <Value>
               |  '++' <Value>
               |  <Value> '--'
               |  <Value> '++'
               |  <Value>


! For use in arithmetic, requires parentheses around expressions to have proper priorities
! e.g. : weight := - (car.weight + passengers.weight)
<Value>  ::= <Literal> <Subscript> ! 2.4.round
               | '(' <SingleThread Exp> ')' <Subscript>
               | Identifier <Subscript>
               | <Value> '[' ']'
               | <Value> '[' <SingleThread Exp> ']' <Subscript>
               | <Value> '<' <Type Arguments> '>' <Subscript>
               | '[' <SingleThread Exp> ']' <Subscript>

<Subscript> ::= ! Empty
               | '.' <Value> ! Property or attribute or function call with no argument
               | '(' <Argument List> ')' ! Function / Method call with arguments

